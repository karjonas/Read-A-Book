// room script file

int correct_block_combo[];
int combo_size = 3;
int combo_idx = 0;

bool puzzle_solved = false;

function hHotspot1_WalkOn()
{
  player.ChangeRoom(7,  200, 150);
}

function room_FirstLoad()
{
  correct_block_combo = new int[combo_size];
  correct_block_combo[0] = 3;
  correct_block_combo[1] = 1;
  correct_block_combo[2] = 6;
  
  bars.Solid = true;
  if(cellar_switch_on)
  {
    RestoreWalkableArea(2);
  }
  else
  {
    RemoveWalkableArea(2);
  }
}

function bars_Look()
{
  player.Say("Those are some sturdy bars");
}

function bars_Talk()
{
  player.Say("Hey bars");
}

function bars_Interact()
{
  player.Walk(bars.X, 150, eBlock, eWalkableAreas);
  player.Say("They won't budge");
}

function room_Load()
{
  if(cellar_switch_on)
  {
    RestoreWalkableArea(2);
    bars.Visible = false;
  }
  else
  {
    RemoveWalkableArea(2);
    bars.Visible = true;
  }
}

function paper_Interact()
{
  if(cellar_switch_on)
  {
    // Lock player inside cell
    cEffectsChar.ChangeRoom(9, bars.X + 10,  150);
    player.Walk(paper.X, paper.Y, eBlock, eWalkableAreas);
    player.Say("It's the third page of the book");
    player.Say("It says: \"Under his home he found a switch that he pulled.\"");
    player.Say("\"He then got trapped inside a cell.\"");
    paper.Visible = false;
    bars.Visible = true;
    cEffectsChar.Say("*SLAM*");
    player.FaceLocation(player.x - 1, player.y, eBlock);
    player.Say("AAAH");
    object[bars.ID].Baseline = 500;
    RemoveWalkableArea(1);
    
    
    // Activate blocks, but set them transparent
    brick_0.Visible = true;
    brick_0.Transparency = 100;
    brick_1.Visible = true;
    brick_1.Transparency = 100;
    brick_2.Visible = true;
    brick_2.Transparency = 100;
    brick_3.Visible = true;
    brick_3.Transparency = 100;
    brick_4.Visible = true;
    brick_4.Transparency = 100;
    brick_5.Visible = true;
    brick_5.Transparency = 100;
    brick_6.Visible = true;
    brick_6.Transparency = 100;
    brick_7.Visible = true;
    brick_7.Transparency = 100;
    brick_8.Visible = true;
    brick_8.Transparency = 100;
  }
}

function tap_at_solid_brick(int x, int y, int idx)
{
  if(puzzle_solved)
    return;
  
  bool right_combo = correct_block_combo[combo_idx] == idx;
  cEffectsChar.ChangeRoom(9, x, y);
  player.Walk(x, y, eBlock, eWalkableAreas);
  
  if(right_combo)
  {
    if(combo_idx == 0)
      cEffectsChar.Say("*CLICK*");
    if(combo_idx == 1)
      cEffectsChar.Say("*CLICK* *CLICK* ");
    if(combo_idx == 2)
    {
      cEffectsChar.Say("*CLICK* *CLICK* *CLICK*");
      puzzle_solved = true;
      player.Say("A hidden door!");
      door_right.Visible = true;
    }
    combo_idx++;
  }
  else if (combo_idx > 0)
  {
    cEffectsChar.Say("*TAP*");
    combo_idx = 0;
  }
  else 
  {
    cEffectsChar.Say("*TAP*");
  }
  
}

function look_at_brick()
{
  player.FaceLocation(player.x + 1, player.y, eBlock);
  player.Say("It's just another brick in the wall");
}

function brick_0_Interact()
{
  tap_at_solid_brick(brick_0.X, brick_0.Y, 0);
}

function brick_1_Interact()
{
  tap_at_solid_brick(brick_1.X, brick_1.Y, 1);
}

function brick_2_Interact()
{
  tap_at_solid_brick(brick_2.X, brick_2.Y, 2);
}

function brick_3_Interact()
{
  tap_at_solid_brick(brick_3.X, brick_3.Y, 3);
}

function brick_4_Interact()
{
  tap_at_solid_brick(brick_4.X, brick_4.Y, 4);
}

function brick_5_Interact()
{
  tap_at_solid_brick(brick_5.X, brick_5.Y, 5);
}

function brick_6_Interact()
{
  tap_at_solid_brick(brick_6.X, brick_6.Y, 6);
}

function brick_7_Interact()
{
  tap_at_solid_brick(brick_7.X, brick_7.Y, 7);
}

function brick_8_Interact()
{
  tap_at_solid_brick(brick_8.X, brick_8.Y, 8);
}

function hHotspot2_WalkOn()
{
  if(puzzle_solved)
  {
    player.ChangeRoom(10, 40, 150);
  }
}

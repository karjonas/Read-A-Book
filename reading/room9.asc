// room script file

int correct_block_combo[];
int combo_size = 9;
int combo_idx = 0;

bool puzzle_solved = false;
bool is_trapped = false;

function hHotspot1_WalkOn()
{
  player.ChangeRoom(7,  200, 150);
}

function room_FirstLoad()
{
  correct_block_combo = new int[combo_size];
  correct_block_combo[0] = brick_0.ID;
  correct_block_combo[1] = brick_3.ID;
  correct_block_combo[2] = brick_6.ID;
  correct_block_combo[3] = brick_7.ID;
  correct_block_combo[4] = brick_8.ID;
  correct_block_combo[5] = brick_5.ID;
  correct_block_combo[6] = brick_2.ID;
  correct_block_combo[7] = brick_1.ID;
  correct_block_combo[8] = brick_4.ID;
  
  bars.Solid = true;
  if(cellar_switch_on)
  {
    RestoreWalkableArea(2);
  }
  else
  {
    RemoveWalkableArea(2);
  }
}

function bars_Look()
{
  player.Say("Those are some sturdy bars");
}

function bars_Talk()
{
  player.Say("Hey bars");
}

function bars_Interact()
{
  player.Walk(bars.X, 150, eBlock, eWalkableAreas);
  player.Say("They won't budge");
}

function room_Load()
{
  if(cellar_switch_on)
  {
    RestoreWalkableArea(2);
    bars.Visible = false;
  }
  else
  {
    RemoveWalkableArea(2);
    bars.Visible = true;
  }
}

function paper_Interact()
{
  if(cellar_switch_on)
  {
    // Lock player inside cell
    is_trapped = true;
    cEffectsChar.ChangeRoom(9, bars.X + 10,  150);
    player.Walk(paper.X, paper.Y, eBlock, eWalkableAreas);
    player.Say("It's the third page of the book");
    player.Say("It says: \"Under his home he found a switch that he pulled.\"");
    player.Say("\"He then got trapped inside a cell.\"");
    paper.Visible = false;
    bars.Visible = true;
    cEffectsChar.Say("*SLAM*");
    player.FaceLocation(player.x - 1, player.y, eBlock);
    player.Say("AAAH");
    object[bars.ID].Baseline = 500;
    RemoveWalkableArea(1);
    
    
    // Activate blocks, but set them transparent
    brick_0.Visible = true;
    brick_1.Visible = true;
    brick_2.Visible = true;
    brick_3.Visible = true;
    brick_4.Visible = true;
    brick_5.Visible = true;
    brick_6.Visible = true;
    brick_7.Visible = true;
    brick_8.Visible = true;
  }
}

function tap_at_solid_brick(int x, int y, int idx,  int obj)
{
  if(puzzle_solved)
    return;
  
  bool right_combo = correct_block_combo[combo_idx] == obj;
  cEffectsChar.ChangeRoom(9, x, y);
  player.Walk(x, y, eBlock, eWalkableAreas);
  
  
  if(right_combo)
  {
    
    object[obj].SetView(7, 1, 0);
    object[obj].Animate(1, 0, eOnce, eNoBlock, eForwards);
    
    cEffectsChar.Say("*CLICK*");
    if(combo_idx == combo_size - 1 )
    {
      puzzle_solved = true;
      door_right.Visible = true;
      player.FaceLocation(player.x + 1, player.y, eBlock); 
      player.Say("A hidden door!");
    }
    combo_idx++;
    
  }
  else if (combo_idx > 0)
  {
    cEffectsChar.Say("*TAP*");
    
    while(combo_idx > 0)
    {
      combo_idx--;
      object[correct_block_combo[combo_idx]].SetView(7, 2, 0);
      object[correct_block_combo[combo_idx]].Animate(2, 0, eOnce, eNoBlock, eForwards);
    }
    
  }
  else 
  {
    object[obj].SetView(7, 2, 0);
    object[obj].Animate(2, 0, eOnce, eNoBlock, eForwards);
    cEffectsChar.Say("*TAP*");
  }
  
}

function look_at_brick()
{
  player.FaceLocation(player.x + 1, player.y, eBlock);
  player.Say("It's just another brick in the wall");
}

function brick_0_Interact()
{
  tap_at_solid_brick(brick_0.X, brick_0.Y, 0, brick_0.ID);
}

function brick_1_Interact()
{
  tap_at_solid_brick(brick_1.X, brick_1.Y, 1, brick_1.ID);
}

function brick_2_Interact()
{
  tap_at_solid_brick(brick_2.X, brick_2.Y, 2, brick_2.ID);
}

function brick_3_Interact()
{
  tap_at_solid_brick(brick_3.X, brick_3.Y, 3, brick_3.ID);
}

function brick_4_Interact()
{
  tap_at_solid_brick(brick_4.X, brick_4.Y, 4, brick_4.ID);
}

function brick_5_Interact()
{
  tap_at_solid_brick(brick_5.X, brick_5.Y, 5, brick_5.ID);
}

function brick_6_Interact()
{
  tap_at_solid_brick(brick_6.X, brick_6.Y, 6, brick_6.ID);
}

function brick_7_Interact()
{
  tap_at_solid_brick(brick_7.X, brick_7.Y, 7, brick_7.ID);
}

function brick_8_Interact()
{
  tap_at_solid_brick(brick_8.X, brick_8.Y, 8,  brick_8.ID);
}

function hHotspot2_WalkOn()
{
  if(puzzle_solved)
  {
    player.ChangeRoom(10, 40, 150);
  }
}

function door_left_Talk()
{
  if(is_trapped)
  {
    player.FaceLocation(player.x - 1, player.y, eBlock);
    player.Say("Hey! Is there anybody there?");
  }
}
